// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    repositories {
        google()// Google's Maven repository
        mavenCentral()
        gradlePluginPortal()
        maven {
            url 'https://developer.huawei.com/repo/' }

    }
    dependencies {
        classpath("com.google.firebase:firebase-crashlytics-gradle:2.8.1")
        classpath("io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.20.0-RC1")
        classpath(libs.android.gradle.plugin)
        classpath(libs.kotlin.gradle.plugin)
        classpath(libs.google.services)
        classpath(libs.huawei.agcp.plugin)
    }
}

plugins {
    alias(libs.plugins.sonarqube)
}
allprojects {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven {
            url 'https://developer.huawei.com/repo/' }
        flatDir {
            dirs 'libs'
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "CleverTap_clevertap-android-sdk"
        property "sonar.organization", "clevertap"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}

task copyTemplates {
    def props = new Properties()
    outputs.upToDateWhen { false }
    props.put("clevertap_android_sdk", libs.version.clevertap.android.sdk.get())
    props.put("clevertap_rendermax_sdk", libs.version.clevertap.rendermax.sdk.get())
    props.put("clevertap_geofence_sdk", libs.version.clevertap.geofence.sdk.get())
    props.put("clevertap_hms_sdk", libs.version.clevertap.hms.sdk.get())
    props.put("clevertap_xiaomi_sdk", libs.version.clevertap.xiaomi.sdk.get())
    props.put("push_templates", libs.version.clevertap.push.templates.sdk.get())
    props.put("play_services_location", libs.play.services.location.get())
    props.put("work_runtime", libs.androidx.work.runtime.get())
    props.put("concurrent_futures",libs.androidx.concurrent.futures.get())
    props.put("push", libs.huawei.push.get())
    props.put("appcompat", libs.androidx.appcompat.get())
    props.put("viewpager", libs.androidx.viewpager.get())
    props.put("material", libs.android.material.get())
    props.put("glide", libs.glide.get())
    props.put("recyclerview", libs.androidx.recyclerview.get())
    props.put("exoplayer", libs.exoplayer.exoplayer.get())
    props.put("exoplayer_ui", libs.exoplayer.ui.get())
    props.put("exoplayer_hls", libs.exoplayer.hls.get())
    props.put("installreferrer", libs.installreferrer.get())
    props.put("firebase_messaging", libs.firebase.messaging.get())
    props.put("androidx_core_core", libs.androidx.core.core.get())
    props.put("agcp", libs.huawei.agcp.plugin.get())
    props.put("com_android_tools_build_gradle", libs.android.gradle.plugin.get())
    props.put("google_services", libs.google.services.get())
    props.put("play_services_ads", libs.play.services.ads.get())
    props.put("clevertap_android_sdk_version", libs.versions.clevertap.android.sdk.get())

    doLast{
        copy {
            from 'templates/README.md'
            into "."
            expand('ext': props.asImmutable())
        }
        copy {
            from 'templates'
            exclude "README.md"
            into "docs/"
            expand('ext': props.asImmutable())
        }
    }
}
